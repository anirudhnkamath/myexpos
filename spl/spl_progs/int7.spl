// mode flag
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 9] = 5;


// ---------------------------------


// save user sp
alias userSP R0;
userSP = SP;


// ---------------------------------


// switch to kernel stack
[PROCESS_TABLE + ( [SYSTEM_STATUS_TABLE + 1] * 16) + 13] = SP;
SP = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 11] * 512 - 1;


// ---------------------------------


// get the file descriptor
alias physicalPageNum R1;
alias offset R2;
alias fileDescPhysicalAddr R3;

physicalPageNum = [PTBR + 2 * ((userSP - 4)/ 512)];
offset = (userSP - 4) % 512;

fileDescPhysicalAddr = (physicalPageNum * 512) + offset;

alias fileDescriptor R4;
fileDescriptor=[fileDescPhysicalAddr];


// ---------------------------------


// check file descriptor
if (fileDescriptor != -2) then

    // invalid ret val
    alias physicalAddrRetVal R5;
    physicalAddrRetVal = ([PTBR + 2 * ((userSP - 1) / 512)] * 512) + ((userSP - 1) % 512);
    [physicalAddrRetVal] = -1;

else

    // get word and print
    alias word R5;
    word = [[PTBR + 2 * ((userSP - 3) / 512)] * 512 + ((userSP - 3) % 512)];

    // set up arguments for device manager and call it

    multipush(R0, R1, R2, R3, R4, R5);
    
    R1 = 3;
    R2 = [SYSTEM_STATUS_TABLE + 1];
    R3 = word;

    call DEVICE_MANAGER;

    multipop(R0, R1, R2, R3, R4, R5);

    alias physicalAddrRetVal R6;
    physicalAddrRetVal = ([PTBR + 2 * (userSP - 1)/ 512] * 512) + ((userSP - 1) % 512);
    [physicalAddrRetVal] = 0;

endif;


// ---------------------------------


// reset user sp
SP = userSP;


// ---------------------------------


// reset mode flag
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 9] = 0;


// ---------------------------------


ireturn;